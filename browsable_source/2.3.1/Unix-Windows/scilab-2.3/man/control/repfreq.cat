
repfreq(1)                         Scilab                          repfreq(1)
Group                                                                   Group
Scilab                                                                 Scilab
NAME
  repfreq - frequency response

CALLING SEQUENCE
  [frq,db,phi] =repfreq(sys,frq)
  [frq,db,phi] =repfreq(sys,fmin,fmax [,step])
  [ [frq,] repf]=repfreq(sys,fmin,fmax [,step])
  [ [frq,] repf]=repfreq(sys,frq)

PARAMETERS

  sys  : syslin list : SIMO linear system

  fmin,fmax      : two real numbers (lower and upper frequency bounds)

  frq            : real vector of frequencies (Hz)

  step           : logarithmic discretization step

  db,phi         : vector of gains (db) and phases at frq (degrees)

  repf           : vector of the complex frequency response

DESCRIPTION
  repfreq returns the frequency response calculation of a linear system. If
  sys(s) is the transfer function of Sys, repf(k) equals sys(s) evaluated at
  s= %i*frq(k)*2*%pi for continuous time systems and at exp(2*%i*%pi*frq(k))
  for discrete time systems.

  db(k) is the magnitude of repf(k) expressed in dB i.e.
  db(k)=20*log(abs(repf(k)))/log(10) and phi(k) is the phase of repf(k)
  expressed in degrees.

  If fmin,fmax,step are input parameters, the response is calculated for the
  vector of frequencies frq given by:
  frq=[exp(log(10)*((log(fmin)/log(10)):step:(log(fmax)/log(10)))) step];

  If step is not given, the output parameter frq is calculated by
  frq=calfrq(sys,fmin,fmax).

EXAMPLE
  A=diag([-1,-2]);B=[1;1];C=[1,1];
  Sys=syslin('c',A,B,C);
  frq=0:0.02:5;w=frq*2*%pi; //frq=frequencies in Hz ;w=frequencies in rad/sec;
  [frq1,db,phi] =repfreq(Sys,frq);
  Systf=ss2tf(Sys)    //Transfer function of Sys
  x=horner(Systf,w(2)*sqrt(-1))    // x is Systf(s) evaluated at s = i w(2)
  rep=20*log(abs(x))/log(10)   //magnitude of x in dB
  db(2)    // same as rep
  ang=atan(imag(x),real(x));   //in rad.
  ang=ang*180/%pi              //in degrees
  phi(2)
  repf=repfreq(Sys,frq);
  repf(2)-x

SEE ALSO
  bode, freq, calfrq, horner, nyquist

AUTHOR
  S. S.
