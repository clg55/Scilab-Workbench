dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision: 0.00 $)dnl
AC_INIT(notice.tex)
SCIDIR=`pwd`
AC_CONFIG_AUX_DIR($SCIDIR/config)
AC_CONFIG_HEADER(routines/machine.h)

dnl Get system type
AC_CANONICAL_HOST

######################
## get SCIDIR pathname
######################

if test -f Path.incl; then
  PATH_INCL=`cat Path.incl`
fi
if test "$PATH_INCL" != "SCIDIR=$SCIDIR"; then
  echo "creating Path.incl"
  cat << EOF > Path.incl
SCIDIR=$SCIDIR
EOF
fi

########################
## removing and cleaning
########################

RM='rm -f'
FILES_TO_CLEAN='*.CKP *.ln *.BAK *.bak core errs ,* *~ *.a .emacs_* tags TAGS make.log MakeOut \#* .*~ *.trace'

##################
## making archives
##################

AR='ar cq'

######################
## use GNU compilers ?
######################

if test "$with_gnu" = yes; then
  AC_CHECK_PROG(GCC,gcc,yes,no)
  if test "$GCC" = no; then
    AC_MSG_ERROR("Unable to configure: gcc compiler not found")
  fi
  AC_CHECK_PROG(G77,g77,yes,no)
  if test "$G77" = no; then
    AC_MSG_ERROR("Unable to configure: g77 compiler not found")
  fi
else
  if test "$with_gcc" = yes; then
    AC_CHECK_PROG(GCC,gcc,yes,no)
    if test "$GCC" = no; then
      AC_MSG_ERROR("Unable to configure: gcc compiler not found")
    fi
  fi
  if test "$with_g77" = yes; then
    AC_CHECK_PROG(G77,g77,yes,no)
    if test "$G77" = no; then
      AC_MSG_ERROR("Unable to configure: g77 compiler not found")
    fi
  fi
fi

#############################################
## Compilers and options according to machine
#############################################

SCILAB_G=g
SCILAB_GS=sh
FGETARG=fgetarg

case "$host" in
  alpha-*-*)
    # standard compilers
    CC=cc
    CC_OPTIONS=-O
    CC_LDFLAGS=
    FC='DECFORT_DEBUG=T;export DECFORT_DEBUG;f77'
    FC_OPTIONS='-O -fpe3 -switch nosqrt_recip'
    FC_LDFLAGS=
    LD=ld
    LD_LDFLAGS=
    if test "$enable_debug" = yes; then 
      CC_OPTIONS=-g
      FC_OPTIONS='-g -fpe3 -switch nosqrt_recip'
    fi
    # GNU compilers
    if test "$GCC" = yes; then
      CC=gcc
      CC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        CC_OPTIONS=-g
      fi
    fi
    if test "$G77" = yes; then
      FC=g77
      FC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        FC_OPTIONS=-g
      fi
    fi
    MAKEFILE_TARGET=Makefile.alpha
    ;;
  rs6000-*-*)
    # standard compilers
    CC=cc
    CC_OPTIONS='-Daix -DSYSV'
    CC_LDFLAGS=
    FC=f77
    FC_OPTIONS='-qcharlen=4096'
    FC_LDFLAGS=
    LD=ld
    LD_LDFLAGS=
    if test "$enable_debug" = yes; then 
      CC_OPTIONS='-g -Daix -DSYSV'
      FC_OPTIONS='-g -qcharlen=4096'
    fi
    # GNU compilers
    if test "$GCC" = yes; then
      CC=gcc
      CC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        CC_OPTIONS=-g
      fi
    fi
    if test "$G77" = yes; then
      FC=g77
      FC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        FC_OPTIONS=-g
      fi
    fi
    MAKEFILE_TARGET=Makefile.aix
    ;;
  sparc-*-sunos*)
    # standard compilers
    CC=cc
    CC_OPTIONS=-O
    CC_LDFLAGS=-Bstatic
    FC=f77
    FC_OPTIONS=-O
    FC_LDFLAGS=-Bstatic
    LD=ld
    LD_LDFLAGS=-Bstatic
    if test "$enable_debug" = yes; then 
      CC_OPTIONS=-g
      FC_OPTIONS=-g
    fi
    # GNU compilers
    if test "$GCC" = yes; then
      CC=gcc
      CC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        CC_OPTIONS=-g
      fi
    fi
    if test "$G77" = yes; then
      FC=g77
      FC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        FC_OPTIONS=-g
      fi
    fi
    MAKEFILE_TARGET=Makefile.sunos
    ;;
  sparc-*-solaris*)
    # standard compilers
    CC=cc
    CC_OPTIONS='-O -DSVR4 -DSYSV'
    CC_LDFLAGS=
    FC=f77
    FC_OPTIONS=-O
    FC_LDFLAGS=
    LD=ld
    LD_LDFLAGS=
    if test "$enable_debug" = yes; then 
      CC_OPTIONS='-g -DSVR4 -DSYSV'
      FC_OPTIONS=-g
    fi
    # GNU compilers
    if test "$GCC" = yes; then
      CC=gcc
      CC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        CC_OPTIONS=-g
      fi
    fi
    if test "$G77" = yes; then
      FC=g77
      FC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        FC_OPTIONS=-g
      fi
    fi
    MAKEFILE_TARGET=Makefile.solaris
    ;;
# Linux a.out
  *-*-linuxaout)
    CC=cc
    CC_OPTIONS='-O -Dlinux -D_POSIX_SOURCE -fwritable-strings'
    CC_LDFLAGS=-lieee
    # script shell f77 have bugs, get ours
    FC=$SCIDIR/bin/f77-linux
    FC_OPTIONS=-O
    FC_LDFLAGS=-lieee
    LD=ld
    LD_LDFLAGS=-lieee
    if test "$enable_debug" = yes; then 
      CC_OPTIONS='-g -Dlinux -D_POSIX_SOURCE -fwritable-strings'
      FC_OPTIONS=-g
    fi
    if test "$G77" = yes; then
      FC=g77
    fi
    DLDLIB='$(SCIDIR)/libs/libdld.a'
    DLD_SUBDIR=dld
    MAKEFILE_TARGET=Makefile.linux
   ;;
# Linux ELF
  *-*-linux)
    CC=cc
    CC_OPTIONS='-O -Dlinux -D_POSIX_SOURCE -fwritable-strings'
    CC_LDFLAGS=-lieee
    # script shell f77 have bugs, get ours
    FC=$SCIDIR/bin/f77-linux
    FC_OPTIONS=-O
    FC_LDFLAGS='-lieee -ldl -rdynamic'
    LD=ld
    LD_LDFLAGS=-lieee
    if test "$enable_debug" = yes; then 
      CC_OPTIONS='-g -Dlinux -D_POSIX_SOURCE -fwritable-strings'
      FC_OPTIONS=-g
    fi
    if test "$G77" = yes; then
      FC=g77
    fi
    MAKEFILE_TARGET=Makefile.linux
   ;;
  mips-*-ultrix*)
    # standard compilers, no longer supported
    CC=cc
    CC_OPTIONS=-O0
    CC_LDFLAGS=
    FC=f77
    FC_OPTIONS='-O0 -fpe1'
    FC_LDFLAGS=
    LD=ld
    LD_LDFLAGS=
    if test "$enable_debug" = yes; then 
      CC_OPTIONS=-g
      FC_OPTIONS='-g -fpe1'
    fi
    SCILAB_G=ultrix
    SCILAB_GS=sh5
    # GNU compilers
    if test "$GCC" = yes; then
      CC=gcc
      CC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        CC_OPTIONS=-g
      fi
    fi
    if test "$G77" = yes; then
      FC=g77
      FC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        FC_OPTIONS=-g
      fi
    fi
    MAKEFILE_TARGET=Makefile.ultrix
    ;;
  mips-sgi-irix*)
    # standard compilers
    CC=cc
    CC_OPTIONS='-O -cckr -DSYSV -DSVR4'
    CC_LDFLAGS=
    FC=f77
    FC_OPTIONS='-O -Nl999 -Nn15000'
    FC_LDFLAGS=
    LD=ld
    LD_LDFLAGS=
    if test "$enable_debug" = yes; then 
      CC_OPTIONS='-g -cckr -DSYSV -DSVR4'
      FC_OPTIONS='-g -Nl999 -Nn15000'
    fi
    # GNU compilers
    if test "$GCC" = yes; then
      CC=gcc
      CC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        CC_OPTIONS=-g
      fi
    fi
    if test "$G77" = yes; then
      FC=g77
      FC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        FC_OPTIONS=-g
      fi
    fi
    MAKEFILE_TARGET=Makefile.sgi
    ;;
  *-*-hpux*)
    # standard compilers
    CC=cc
    CC_OPTIONS='-O -DSYSV -Dhpux'
    CC_LDFLAGS='-Wl,-a,archive'
    FC=fort77
    FC_OPTIONS='-O +Obb1200 +E4'
    FC_LDFLAGS='-Wl,-a,archive'
    LD=ld
    LD_LDFLAGS='-a archive'
    if test "$enable_debug" = yes; then 
      CC_OPTIONS='-g -DSYSV -Dhpux'
      FC_OPTIONS='-g +E4'
    fi
    # GNU compilers
    if test "$GCC" = yes; then
      CC=gcc
      CC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        CC_OPTIONS=-g
      fi
    fi
    if test "$G77" = yes; then
      FC=g77
      FC_OPTIONS=-O
      if test "$enable_debug" = yes; then 
        FC_OPTIONS=-g
      fi
    fi
    MAKEFILE_TARGET=Makefile.hpux
    SCILAB_G=hpux
    FGETARG=fgetarg-hpux
    ;;
  *)
    AC_MSG_ERROR("Unable to configure for host $host")
    ;;
esac

# flag for possible compilations in configure
CFLAGS=$CC_OPTIONS

############################
## test for various programs
############################

AC_PROG_RANLIB
AC_PROG_INSTALL

####################
## test for X Window
####################

AC_PATH_XTRA

if test "$no_x" = yes; then
  AC_MSG_ERROR("X Window not found")
fi

###################
## find X11 release
###################

AC_CACHE_CHECK("for X11 release",ac_cv_X11_release,
[
cat << EOF > testX11.c
#include <X11/Xlib.h>
main()
{
#ifdef XlibSpecificationRelease
  if (XlibSpecificationRelease == 5)
    printf("R5\n");
  else if (XlibSpecificationRelease == 6)
    printf("R6\n");
  else
    printf("R?\n");
#else
  printf("R4\n");
#endif
}
EOF
eval "$CC $X_CFLAGS -o testX11 testX11.c > /dev/null 2>&1"
ac_cv_X11_release=`./testX11`
$RM testX11.c testX11
])

######################################
## test for Xaw and Xmu Athena widgets
######################################

if test "$with_local_Xaw" = yes; then
  if test "$ac_cv_X11_release" = R4; then
    AC_MSG_ERROR("Local Xaw and Xmu Libraries cannot be used with X11R4")
  else
    XAWLIB='$(SCIDIR)/libs/libXaw.a'
    XMULIB='$(SCIDIR)/libs/libXmu.a'
    XAW_LOCAL_LIB='$(SCIDIR)/libs/libXaw.a $(SCIDIR)/libs/libXmu.a'
    XAW_LOCAL_INCLUDES='-I$(SCIDIR)/routines'
    XAW_LOCAL_SUBDIR=X11
  fi
else
  ac_save_LDFLAGS="$LDFLAGS"
  if test -n "$x_libraries"; then
    LDFLAGS="$LDFLAGS -L$x_libraries"
  fi
  AC_CHECK_LIB(Xmu,main,[XAthena=yes],[XAthena=no],-lXt -lXext $X_PRE_LIBS -lX11 $X_EXTRA_LIBS)
  AC_CHECK_LIB(Xaw,main,[XAthena=yes],[XAthena=no],-lXmu -lXt -lXext $X_PRE_LIBS -lX11 $X_EXTRA_LIBS)
  LDFLAGS="$ac_save_LDFLAGS"
  if test "$XAthena" = no; then
    AC_MSG_WARN("Xaw and/or Xmu Library missing")
    if test "$ac_cv_X11_release" = R4; then
      AC_MSG_ERROR("Local Xaw and Xmu Libraries cannot be used with X11R4")
    else
      AC_MSG_WARN("Using Local Xaw and Xmu Libraries")
      XAWLIB='$(SCIDIR)/libs/libXaw.a'
      XMULIB='$(SCIDIR)/libs/libXmu.a'
      XAW_LOCAL_LIB='$(SCIDIR)/libs/libXaw.a $(SCIDIR)/libs/libXmu.a'
      XAW_LOCAL_INCLUDES='-I$(SCIDIR)/routines'
      XAW_LOCAL_SUBDIR=X11
    fi
  else
    XAWLIB=-lXaw
    XMULIB=-lXmu
  fi
fi

#######################
## test for underscores
#######################

AC_MSG_CHECKING("for leading and/or trailing underscores")
cat << EOF > pipof.f
       subroutine pipof
       end
EOF

eval "$FC -c pipof.f > /dev/null 2>&1"

FC_LEADING_UNDERSCORE=no
FC_TRAILING_UNDERSCORE=no

output=`nm pipof.o|grep _pipof 2>&1`
if test ! -z "$output"; then
  FC_LEADING_UNDERSCORE=yes
  FC_TRAILING_UNDERSCORE=no
fi

output=`nm pipof.o|grep pipof_ 2>&1`
if test ! -z "$output"; then
  FC_LEADING_UNDERSCORE=no
  FC_TRAILING_UNDERSCORE=yes
fi

output=`nm pipof.o|grep _pipof_ 2>&1`
if test ! -z "$output"; then
  FC_LEADING_UNDERSCORE=yes
  FC_TRAILING_UNDERSCORE=yes
fi

if test "$FC_LEADING_UNDERSCORE" = yes; then
  AC_DEFINE(WLU)
fi
if test "$FC_TRAILING_UNDERSCORE" = yes; then
  AC_DEFINE(WTU)
fi

$RM pipof.f pipof.o

AC_MSG_RESULT("$FC_LEADING_UNDERSCORE $FC_TRAILING_UNDERSCORE")

#####################
## test for sharpsign
#####################

AC_MSG_CHECKING("for use of sharpsign in CPP")
AC_TRY_COMPILE([#define C2F(name) name##_],
  [C2F(toto)()],  
  AC_MSG_RESULT(yes)
  AC_DEFINE(USE_SHARP_SIGN),
  AC_MSG_RESULT(no))

##############################################################
## test for functions in standard C library and C math library
##############################################################
AC_CHECK_LIB(m,main)
AC_CHECK_FUNCS(exp10)
AC_CHECK_FUNCS(getwd)

##################
## termcap library
##################
TERMCAPLIB=no
AC_CHECK_LIB(termcap,main,
  [TERMCAPLIB=-ltermcap])
if test "$TERMCAPLIB" = no; then
  AC_CHECK_LIB(termlib,main,
  [TERMCAPLIB=-ltermlib])
fi
if test "$TERMCAPLIB" = no; then
  AC_CHECK_LIB(curses,main,
  [TERMCAPLIB=-lcurses])
fi

if test "$TERMCAPLIB" = no; then
  AC_MSG_ERROR("no termcap library found")
fi

#########################
## substituting variables
#########################

AC_SUBST(SCIDIR)
AC_SUBST(RM)
AC_SUBST(AR)
AC_SUBST(FILES_TO_CLEAN)
AC_SUBST(CC)
AC_SUBST(CC_OPTIONS)
AC_SUBST(CC_LDFLAGS)
AC_SUBST(FC)
AC_SUBST(FC_OPTIONS)
AC_SUBST(FC_LDFLAGS)
AC_SUBST(LD)
AC_SUBST(LD_LDFLAGS)
AC_SUBST(SCILAB_G)
AC_SUBST(SCILAB_GS)
AC_SUBST(FGETARG)
AC_SUBST(DLDLIB)
AC_SUBST(DLD_SUBDIR)
AC_SUBST(XAWLIB)
AC_SUBST(XMULIB)
AC_SUBST(XAW_LOCAL_SUBDIR)
AC_SUBST(XAW_LOCAL_INCLUDES)
AC_SUBST(XAW_LOCAL_LIB)
AC_SUBST(TERMCAPLIB)
AC_SUBST(MAKEFILE_TARGET)

AC_OUTPUT(Makefile.incl Makefile scripts/Makefile routines/sun/Makefile)
