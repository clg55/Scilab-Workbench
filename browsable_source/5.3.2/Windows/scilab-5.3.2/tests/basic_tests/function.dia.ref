 
//Checks syntactical aspects  related to functions
 
funcprot(0)
 
//
 
//               Various calling sequences
 
//               -------------------------
 
function y=foo(x)
 y=x^2
endfunction
 
if foo(2)<>4  then bugmes();quit;end
 
//
 
function y=foo(a,b)
 y=a+b
endfunction
 
if foo(2,3)<>5  then bugmes();quit;end
 
//
 
function [y,z]=foo(a)
 y=a^2
 z=a^3
endfunction
 
[y,z]=foo(2);if y<>4|z<>8 then bugmes();quit;end
 
//
 
function y=foo()
 y=2
endfunction
 
if foo()<>2  then bugmes();quit;end
 
//
 
function y=foo
 y=2
endfunction
 
if foo()<>2  then bugmes();quit;end
 
//
 
function []=foo
 y=resume(2)
endfunction
 
foo();if y<>2  then bugmes();quit;end
 
//
 
function foo
 y=resume(3)
endfunction
 
foo();if y<>3  then bugmes();quit;end
 
 
//
 
//               Various line splits
 
//               -------------------
 
function y=foo(x),y=x^2
endfunction
 
if foo(2)<>4  then bugmes();quit;end
 
//
 
function y=foo(x),y=3*x,endfunction
 
if foo(2)<>6  then bugmes();quit;end
 
//
 
function y=foo(x)
  z=x^2
  function y=foo1(x)
    y=x+1
  endfunction
  y=foo1(z)
endfunction
 
if foo(2)<>5  then bugmes();quit;end
 
//
 
//               Combined with deff
 
//               -------------------
 
function y=foo(x)
  z=x^2
  deff('y=foo1(x)','y=x+1')
  y=foo1(z)
endfunction
 
if foo(2)<>5  then bugmes();quit;end
 
//
 
deff('y=foo(x)',[
    'z=x^2'
    'function y=foo1(x)'
    'y=x+1'
    'endfunction'
    'y=foo1(z)'])
 
if foo(2)<>5  then bugmes();quit;end
 
		
 
function y=foo(x),y=x+1
endfunction;if foo(2)<>3  then bugmes();quit;end
 
//
 
//               Combined with exec
 
//               -------------------
 
function foo
  z=9;
  function y=foo1(x)
    y=x+1;
  endfunction
  y=foo1(z);
endfunction
 
 
//
 
 
//               Combined with control instructions
 
//               ----------------------------------
 
if %t then
  function  y=foo(x)
    y=sin(x)
  endfunction
else
  function  y=foo(x)
    y=1
  endfunction
end
 
if foo(1)<>sin(1)  then bugmes();quit;end
 
//
 
if %t then
  function  y=foo(x)
  if x==0 then
    y=1
  else
    y=sin(x)/x
  end
  endfunction
else
  function  y=foo(x)
    y=1
  endfunction
end
 
if foo(0)<>1   then bugmes();quit;end
 
if foo(2)<>sin(2)/2   then bugmes();quit;end
 
//
 
z=0;
 
for k=1:2
 function y=foo()
   y=k
 endfunction
 z=z+foo();
end
 
if z<>3 then bugmes();quit;end
 
//
 
z=0;for k=1:2
 function y=foo(),y=k,endfunction
 z=z+foo();
end
 
if z<>3 then bugmes();quit;end
 
//
 
z=0;for k=1:2, function y=foo(),y=k,endfunction
 z=z+foo();
end
 
if z<>3 then bugmes();quit;end
 
 
